plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.0'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'org.flywaydb.flyway' version "${flywayVersion}"
    id 'jacoco'
}

group = 'com.ahmad'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.apache.commons:commons-csv:1.10.0'
    implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
    implementation 'io.r2dbc:r2dbc-postgresql:0.8.13.RELEASE'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis-reactive:3.2.0'
    implementation 'io.lettuce:lettuce-core:6.3.0.RELEASE'

    runtimeOnly 'org.postgresql:postgresql'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'com.squareup.okhttp3:mockwebserver'

}

tasks.named('test') {
    useJUnitPlatform()
}

test {
    testLogging {
        events "passed", "failed", "skipped"
    }
    finalizedBy jacocoTestReport // report is always generated after tests run
}

jacoco {
    toolVersion = "0.8.9"
//    reportsDirectory = layout.buildDirectory.dir('jacocoCsv')
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
    reports {
        xml.required = false
        csv.required = true
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }

}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.6
            }
        }
    }
}

check {
    dependsOn += jacocoTestCoverageVerification
}

flyway {
    url = "jdbc:postgresql://$flywayHost:$flywayPort/$flywayDatabase"
    user = flywayUsername
    password = flywayPassword
    locations = ["filesystem:src/main/resources/db"]
}

build.dependsOn flywayMigrate